apply plugin: 'java'
apply plugin: 'scala'
apply plugin: 'eclipse'

group = 'com.artclod'
version = '0.0.0-SNAPSHOT'
sourceCompatibility = JavaVersion.VERSION_1_7
String scalaVer = '2.10'
String scalaVersion = "$scalaVer" + '.0' // NOTE this version may not match the plugin version it needs to be kept up to date manually

sourceSets.main.scala.srcDir "src/main/java"
sourceSets.main.java.srcDirs = []

repositories {
	mavenCentral()
}

dependencies {
	compile     group: 'org.scala-lang',     name: 'scala-library',         version: "$scalaVersion"
	compile     group: 'org.scalaz',         name: "scalaz-core_$scalaVer", version: '6.0.4'
	compile     group: 'com.h2database',     name: "h2",                    version: '1.3.172'
                                                                          
	testCompile group: 'junit',              name: 'junit-dep',             version: '4.10'
	testCompile group: 'org.hamcrest',       name: 'hamcrest-core',         version: '1.2.1'
	testCompile group: 'org.mockito',        name: 'mockito-core',          version: '1.8.5'
}

eclipse {
  classpath {
    containers "org.scala-ide.sdt.launching.SCALA_CONTAINER"
    file {
      whenMerged { classpath ->
        def scalaLib = classpath.entries.findAll {it.toString().contains("scala-library")}
        // Avoid problems having two scala-library in the path (the Scala IDE plugin brings one and gradle needs one for non IDE purposes)
        classpath.entries.removeAll(scalaLib)
      }
    }
  }  
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.6'
}
