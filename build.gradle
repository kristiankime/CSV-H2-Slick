apply plugin: 'java'
apply plugin: 'scala'
apply plugin: 'groovy'
apply plugin: 'eclipse'

group = 'com.artclod'
version = '0.0.0-SNAPSHOT'
sourceCompatibility = JavaVersion.VERSION_1_7
String scalaVer = '2.10'
String scalaVersion = "$scalaVer" + '.1' // NOTE this version may not match the plugin version it needs to be kept up to date manually

sourceSets.main.scala.srcDir "src/main/java"
sourceSets.main.java.srcDirs = []

sourceSets {
	example {
		scala { srcDir 'example/scala' }
	}
}

repositories {
	mavenCentral()
}

dependencies {
	compile     group: 'org.scala-lang',          name: 'scala-library',         version: scalaVersion
	compile     group: 'org.scalaz',              name: "scalaz-core_$scalaVer", version: '6.0.4'
	compile     group: 'com.h2database',          name: 'h2',                    version: '1.3.172'
	compile     group: 'com.typesafe.slick',      name: "slick_$scalaVer",       version: '1.0.0'
    compile     group: 'org.slf4j',               name: 'slf4j-nop',             version: '1.6.4'
//    compile     group: 'org.slf4j',               name: 'slf4j-api',             version: '1.6.4'
//    compile     group: 'ch.qos.logback',          name: 'logback-classic',       version: '1.0.0'
//    compile     group: 'ch.qos.logback',          name: 'logback-core',          version: '1.0.0'                   
	                
	testCompile group: 'org.scalatest',      name: "scalatest_$scalaVer",   version: '1.9.1'                                                 
	testCompile group: 'junit',              name: 'junit-dep',             version: '4.10'
	testCompile group: 'org.hamcrest',       name: 'hamcrest-core',         version: '1.2.1'
	testCompile group: 'org.mockito',        name: 'mockito-core',          version: '1.8.5'
}

eclipse {
  classpath {
    containers "org.scala-ide.sdt.launching.SCALA_CONTAINER"
    file {
      whenMerged { classpath ->
        def scalaLib = classpath.entries.findAll {it.toString().contains("scala-library")}
        // Avoid problems having two scala-library in the path (the Scala IDE plugin brings one and gradle needs one for non IDE purposes)
        classpath.entries.removeAll(scalaLib)
      }
    }
  }  
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.6'
}

// http://issues.gradle.org/browse/GRADLE-1032
test << {
    ant.taskdef(name: 'scalatest', classname: 'org.scalatest.tools.ScalaTestAntTask', classpath: classpath.asPath)
    ant.scalatest(runpath: testClassesDir, haltonfailure: 'true', fork: 'false') {
        reporter(type: 'stdout')
    }
}
